<?xml version="1.0" encoding="utf-8"?>
<CLAS CLSNAME="ZCL_EXCEL_WRITER_HUGE_FILE" VERSION="1" LANGU="D" DESCRIPT="Create huge XLSX file" CATEGORY="00" EXPOSURE="2" STATE="1" RELEASE="0" CLSFINAL="X" CLSCCINCL="X" FIXPT="X" UNICODE="X" CLSBCCAT="00" DURATION_TYPE="0 " RISK_LEVEL="0 " ZSAPLINK_PLUGIN_MAJOR_VERSION="0 " ZSAPLINK_PLUGIN_MINOR_VERSION="1 " ZSAPLINK_PLUGIN_BUILD_VERSION="0 " ZSAPLINK_PLUGIN_INFO1="ZSAPLINK_CLASS is part of the main ZSAPLINK project --&gt; This plugin found there instead of ZSAPLINK_PLUGINS projects" ZSAPLINK_PLUGIN_INFO2="SAPLINK homepage: https://www.assembla.com/spaces/saplink/wiki" ZSAPLINK_PLUGIN_INFO3="Download from https://www.assembla.com/code/saplink/subversion/nodes" ZSAPLINK_PLUGIN_INFO4="and navigate to:  trunk -&gt; core -&gt; ZSAPLINK -&gt; CLAS -&gt; ZSAPLINK_CLASS.slnk" REFCLSNAME="ZCL_EXCEL_WRITER_2007">
 <types CLSNAME="ZCL_EXCEL_WRITER_HUGE_FILE" CMPNAME="TY_CELL" VERSION="1" LANGU="D" DESCRIPT="Cell data for callback from transformation" EXPOSURE="2" STATE="1" EDITORDER="1 " TYPTYPE="4" SRCROW1="10 " SRCCOLUMN1="4 " SRCROW2="16 " SRCCOLUMN2="19 " TYPESRC_LENG="0 " TYPESRC='BEGIN OF ty_cell,
        name TYPE c LENGTH 10, &quot;AAA1234567&quot;
        style TYPE i,
        type TYPE c LENGTH 9,
        formula TYPE string,
        value TYPE string,
      END OF ty_cell
'/>
 <localImplementation>*&quot;* use this source file for the definition and implementation of
*&quot;* local helper classes, interface definitions and type
*&quot;* declarations</localImplementation>
 <localTypes>*&quot;* use this source file for any type of declarations (class
*&quot;* definitions, interfaces or type declarations) you need for
*&quot;* components in the private section</localTypes>
 <localMacros>*&quot;* use this source file for any macro definitions you need
*&quot;* in the implementation part of the class</localMacros>
 <attribute CLSNAME="ZCL_EXCEL_WRITER_HUGE_FILE" CMPNAME="CELLS" VERSION="1" LANGU="D" DESCRIPT="Cell data for callback from transformation" EXPOSURE="2" STATE="1" EDITORDER="1 " ATTDECLTYP="0" ATTEXPVIRT="0" TYPTYPE="4" SRCROW1="19 " SRCCOLUMN1="4 " SRCROW2="19 " SRCCOLUMN2="39 " TYPESRC_LENG="37 " TYPESRC="cells TYPE STANDARD TABLE OF ty_cell
"/>
 <attribute CLSNAME="ZCL_EXCEL_WRITER_HUGE_FILE" CMPNAME="WORKSHEET" VERSION="1" LANGU="D" DESCRIPT="Reference needed for GET_CELL" EXPOSURE="0" STATE="1" EDITORDER="1 " ATTDECLTYP="0" ATTEXPVIRT="0" TYPTYPE="3" TYPE="ZCL_EXCEL_WORKSHEET" SRCROW1="0 " SRCCOLUMN1="0 " SRCROW2="0 " SRCCOLUMN2="0 " TYPESRC_LENG="0 "/>
 <inheritance CLSNAME="ZCL_EXCEL_WRITER_HUGE_FILE" REFCLSNAME="ZCL_EXCEL_WRITER_2007" VERSION="1" STATE="1">
  <redefinition CLSNAME="ZCL_EXCEL_WRITER_HUGE_FILE" REFCLSNAME="ZCL_EXCEL_WRITER_2007" VERSION="1" MTDNAME="CREATE_XL_SHAREDSTRINGS" EXPOSURE="1"/>
  <redefinition CLSNAME="ZCL_EXCEL_WRITER_HUGE_FILE" REFCLSNAME="ZCL_EXCEL_WRITER_2007" VERSION="1" MTDNAME="CREATE_XL_SHEET" EXPOSURE="1"/>
 </inheritance>
 <method CLSNAME="ZCL_EXCEL_WRITER_2007" CMPNAME="CREATE_XL_SHAREDSTRINGS" VERSION="0" EXPOSURE="0" STATE="0" EDITORDER="0 " DISPID="0 " MTDTYPE="0" MTDDECLTYP="0" BCMTDCAT="00" BCMTDSYN="0">
  <source>METHOD create_xl_sharedstrings.
*
* Redefinition using simple transformation instead of CL_IXML
*
** Constant node name

  TYPES:
    BEGIN OF ts_root,
      count        TYPE string,
      unique_count TYPE string,
    END OF ts_root.

  DATA:
    lv_last_allowed_char TYPE char1,
    lv_invalid           TYPE string.

  DATA:
    lo_iterator  TYPE REF TO cl_object_collection_iterator,
    lo_worksheet TYPE REF TO zcl_excel_worksheet.

  DATA:
    ls_root          TYPE ts_root,
    lt_cell_data     TYPE zexcel_t_cell_data_unsorted,
    ls_shared_string TYPE zexcel_s_shared_string,
    lv_sytabix       TYPE sytabix.

  FIELD-SYMBOLS:
    &lt;sheet_content&gt;     TYPE zexcel_s_cell_data.

**********************************************************************
* STEP 0: Build Regex for invalid characters
  CASE cl_abap_char_utilities=&gt;charsize.
    WHEN 1.lv_last_allowed_char = cl_abap_conv_in_ce=&gt;uccpi( 255 ).  &quot; FF     in non-Unicode
    WHEN 2.lv_last_allowed_char = cl_abap_conv_in_ce=&gt;uccpi( 65533 ).&quot; FFFD   in Unicode
  ENDCASE.
  CONCATENATE &apos;[^\n\t\r -&apos; lv_last_allowed_char &apos;]&apos; INTO lv_invalid.

**********************************************************************
* STEP 1: Collect strings from each worksheet

  lo_iterator = excel-&gt;get_worksheets_iterator( ).

  WHILE lo_iterator-&gt;if_object_collection_iterator~has_next( ) EQ abap_true.
    lo_worksheet ?= lo_iterator-&gt;if_object_collection_iterator~get_next( ).
    APPEND LINES OF lo_worksheet-&gt;sheet_content TO lt_cell_data.
  ENDWHILE.

  DELETE lt_cell_data WHERE cell_formula IS NOT INITIAL &quot; delete formula content
                         OR data_type    NE &apos;s&apos;.        &quot; MvC: Only shared strings

  ls_root-count = lines( lt_cell_data ).
  CONDENSE ls_root-count.

  SORT lt_cell_data BY cell_value.
  DELETE ADJACENT DUPLICATES FROM lt_cell_data COMPARING cell_value.

  ls_root-unique_count = lines( lt_cell_data ).
  CONDENSE ls_root-unique_count.

  LOOP AT lt_cell_data ASSIGNING &lt;sheet_content&gt;.

    lv_sytabix = sy-tabix - 1.
    MOVE lv_sytabix                 TO ls_shared_string-string_no.
    MOVE &lt;sheet_content&gt;-cell_value TO ls_shared_string-string_value.
    REPLACE ALL OCCURRENCES OF REGEX lv_invalid
         IN ls_shared_string-string_value WITH ` `.
    APPEND ls_shared_string TO shared_strings.

  ENDLOOP.

**********************************************************************
* STEP 2: Create XML

  CALL TRANSFORMATION zexcel_tr_shared_strings
    SOURCE root = ls_root
           shared_strings = shared_strings
    OPTIONS xml_header = &apos;full&apos;
    RESULT XML ep_content.


ENDMETHOD.</source>
 </method>
 <method CLSNAME="ZCL_EXCEL_WRITER_2007" CMPNAME="CREATE_XL_SHEET" VERSION="0" EXPOSURE="0" STATE="0" EDITORDER="0 " DISPID="0 " MTDTYPE="0" MTDDECLTYP="0" BCMTDCAT="00" BCMTDSYN="0">
  <source>METHOD create_xl_sheet.
*
* Build Sheet#.xml with Simple Transformation ZEXCEL_TR_SHEET
*
* This is an adaption of ZCL_EXCEL_WRITER_2007.
* Not all features are supported, notably the autofilter settings,
* conditional formatting and sheet protection.
*
* Bug reports to marcus.voncube AT deutschebahn.com
*
  TYPES:
    lty_bool TYPE c LENGTH 5.

  CONSTANTS:
    lc_false              TYPE lty_bool   VALUE &apos;false&apos;,    &quot;#EC NEEDED
    lc_true               TYPE lty_bool   VALUE &apos;true&apos;,
    lc_zero               TYPE c LENGTH 1 VALUE &apos;0&apos;,
    lc_one                TYPE c LENGTH 1 VALUE &apos;1&apos;,
    lc_default_col_width  TYPE float      VALUE &apos;9.10&apos;.

  TYPES:
    BEGIN OF lty_column,
      min          TYPE i,
      max          TYPE i,
      width        TYPE float,
      hidden       TYPE lty_bool,
      customwidth  TYPE lty_bool,
      bestfit      TYPE lty_bool,
      collapsed    TYPE lty_bool,
      outlinelevel TYPE i,
      style        TYPE i,
    END OF lty_column,

    BEGIN OF lty_row,
      row          TYPE i,
      index        TYPE i,
      spans        TYPE c LENGTH 11,  &quot;12345:12345&quot;
      hidden       TYPE lty_bool,
      customheight TYPE lty_bool,
      height       TYPE float,
      collapsed    TYPE lty_bool,
      outlinelevel TYPE i,
      customformat TYPE lty_bool,
      style        TYPE i,
    END OF lty_row,

    BEGIN OF lty_mergecell,
      ref          TYPE c LENGTH 21, &quot;AAA1234567:BBB1234567&quot;
    END OF lty_mergecell,

    BEGIN OF lty_hyperlink,
      ref          TYPE string,
      location     TYPE string,
      r_id         TYPE string,
    END OF lty_hyperlink,

    BEGIN OF lty_table,
      r_id         TYPE string,
    END OF lty_table,

    BEGIN OF lty_table_area,
      left         TYPE i,
      right        TYPE i,
      top          TYPE i,
      bottom       TYPE i,
    END OF lty_table_area,

    BEGIN OF ty_missing_columns,
      first_column TYPE zexcel_cell_column,
      last_column  TYPE zexcel_cell_column,
    END OF ty_missing_columns.

*
* Root node for transformation
*
  DATA:
    BEGIN OF l_worksheet,
      dimension                 TYPE string,
      tabcolor                  TYPE string,
      summarybelow              TYPE c,
      summaryright              TYPE c,
      fittopage                 TYPE c,
      showzeros                 TYPE c,
      tabselected               TYPE c,
      zoomscale                 TYPE i,
      zoomscalenormal           TYPE i,
      zoomscalepageview         TYPE i,
      zoomscalesheetview        TYPE i,
      workbookviewid            TYPE c,
      showgridlines             TYPE c,
      showrowcolheaders         TYPE c,
      activepane                TYPE string,
      state                     TYPE string,
      ysplit                    TYPE i,
      xsplit                    TYPE i,
      topleftcell               TYPE c LENGTH 10,
      activecell                TYPE c LENGTH 10,
      customheight              TYPE lty_bool,
      defaultrowheight          TYPE float,
      defaultcolwidth           TYPE float,
      outlinelevelrow           TYPE i,
      outlinelevelcol           TYPE i,
      cols                      TYPE STANDARD TABLE OF lty_column,
      rows                      TYPE STANDARD TABLE OF lty_row,
      mergecells_count          TYPE i,
      mergecells                TYPE STANDARD TABLE OF lty_mergecell,
      hyperlinks_count          TYPE i,
      hyperlinks                TYPE STANDARD TABLE OF lty_hyperlink,
      BEGIN OF printoptions,
        gridlines               TYPE lty_bool,
        horizontalcentered      TYPE lty_bool,
        verticalcentered        TYPE lty_bool,
      END OF printoptions,
      BEGIN OF pagemargins,
        left                    TYPE zexcel_dec_8_2,
        right                   TYPE zexcel_dec_8_2,
        top                     TYPE zexcel_dec_8_2,
        bottom                  TYPE zexcel_dec_8_2,
        header                  TYPE zexcel_dec_8_2,
        footer                  TYPE zexcel_dec_8_2,
      END OF pagemargins,
      BEGIN OF pagesetup,
        blackandwhite           TYPE c,
        cellcomments            TYPE string,
        copies                  TYPE i,
        draft                   TYPE c,
        errors                  TYPE string,
        firstpagenumber         TYPE i,
        fittopage               TYPE c,
        fittoheight             TYPE i,
        fittowidth              TYPE i,
        horizontaldpi           TYPE i,
        orientation             TYPE string,
        pageorder               TYPE string,
        paperheight             TYPE string,
        papersize               TYPE i,
        paperwidth              TYPE string,
        scale                   TYPE i,
        usefirstpagenumber      TYPE c,
        useprinterdefaults      TYPE c,
        verticaldpi             TYPE i,
      END OF pagesetup,
      BEGIN OF headerfooter,
        differentoddeven        TYPE c,
        oddheader               TYPE string,
        oddfooter               TYPE string,
        evenheader              TYPE string,
        evenfooter              TYPE string,
      END OF headerfooter,
      drawings                  TYPE string,
      tables_count              TYPE i,
      tables                    TYPE STANDARD TABLE OF lty_table,
    END OF l_worksheet.

*
* Local data
*
  DATA:
    lo_iterator                 TYPE REF TO cl_object_collection_iterator,
    lo_table                    TYPE REF TO zcl_excel_table,
    row_dimension               TYPE REF TO zcl_excel_worksheet_rowdimensi,
    default_col_dimension       TYPE REF TO zcl_excel_worksheet_columndime,
    default_row_dimension       TYPE REF TO zcl_excel_worksheet_rowdimensi,
    lv_value                    TYPE string,
    lv_index                    TYPE i,
    lv_spans                    TYPE string,
    lt_range_merge              TYPE string_table,
    lv_column                   TYPE zexcel_cell_column,
    lv_style_guid               TYPE zexcel_cell_style,
    ls_last_row                 TYPE zexcel_s_cell_data,
    lv_freeze_cell_row          TYPE zexcel_cell_row,
    lv_freeze_cell_column       TYPE zexcel_cell_column,
    lv_freeze_cell_column_alpha TYPE zexcel_cell_column_alpha,
    column_dimensions           TYPE zexcel_t_worksheet_columndime,
    row_dimensions              TYPE zexcel_t_worksheet_rowdimensio,
    lv_relation_id              TYPE i VALUE 0,
    outline_level_row           TYPE i VALUE 0,
    outline_level_col           TYPE i VALUE 0,
    col_count                   TYPE int4,
    lt_table_areas              TYPE SORTED TABLE OF lty_table_area
                                     WITH NON-UNIQUE KEY left right top bottom,
    ls_table_area               LIKE LINE OF lt_table_areas,
    lts_sorted_columns          TYPE SORTED TABLE OF zexcel_cell_column
                                     WITH UNIQUE KEY table_line,
    t_missing_columns           TYPE STANDARD TABLE OF ty_missing_columns
                                     WITH NON-UNIQUE DEFAULT KEY,
    missing_column              LIKE LINE OF t_missing_columns,
    lo_link                     TYPE REF TO zcl_excel_hyperlink,
    lo_drawings                 TYPE REF TO zcl_excel_drawings.

  FIELD-SYMBOLS:
    &lt;sheet_content&gt;             TYPE zexcel_s_cell_data,
    &lt;range_merge&gt;               LIKE LINE OF lt_range_merge,
    &lt;column_dimension&gt;          TYPE zexcel_s_worksheet_columndime,
    &lt;row_dimension&gt;             TYPE zexcel_s_worksheet_rowdimensio,
    &lt;col&gt;                       TYPE lty_column,
    &lt;row&gt;                       TYPE lty_row,
    &lt;hyperlink&gt;                 TYPE lty_hyperlink,
    &lt;mergecell&gt;                 TYPE lty_mergecell,
    &lt;table&gt;                     TYPE lty_table.

**********************************************************************
* STEP 1: Fill root node
*
  l_worksheet-tabcolor      = io_worksheet-&gt;tabcolor-rgb.
  l_worksheet-summarybelow  = io_worksheet-&gt;zif_excel_sheet_properties~summarybelow.
  l_worksheet-summaryright  = io_worksheet-&gt;zif_excel_sheet_properties~summaryright.

  IF io_worksheet-&gt;sheet_setup-&gt;fit_to_page IS NOT INITIAL.
    l_worksheet-fittopage   = lc_one.
  ENDIF.

  l_worksheet-dimension     = io_worksheet-&gt;get_dimension_range( ).

  IF io_worksheet-&gt;zif_excel_sheet_properties~show_zeros EQ abap_true.
    l_worksheet-showzeros   = lc_one.
  ELSE.
    l_worksheet-showzeros   = lc_zero.
  ENDIF.

  IF iv_active = abap_true
    OR io_worksheet-&gt;zif_excel_sheet_properties~selected EQ abap_true.
    l_worksheet-tabselected = lc_one.
  ELSE.
    l_worksheet-tabselected = lc_zero.
  ENDIF.

  IF io_worksheet-&gt;zif_excel_sheet_properties~zoomscale GT 400.
    io_worksheet-&gt;zif_excel_sheet_properties~zoomscale = 400.
  ELSEIF io_worksheet-&gt;zif_excel_sheet_properties~zoomscale LT 10.
    io_worksheet-&gt;zif_excel_sheet_properties~zoomscale = 10.
  ENDIF.
  l_worksheet-zoomscale = io_worksheet-&gt;zif_excel_sheet_properties~zoomscale.

  IF io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_normal NE 0.
    IF io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_normal GT 400.
      io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_normal = 400.
    ELSEIF io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_normal LT 10.
      io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_normal = 10.
    ENDIF.
    l_worksheet-zoomscalenormal = io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_normal.
  ENDIF.

  IF io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_pagelayoutview NE 0.
    IF io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_pagelayoutview GT 400.
      io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_pagelayoutview = 400.
    ELSEIF io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_pagelayoutview LT 10.
      io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_pagelayoutview = 10.
    ENDIF.
    l_worksheet-zoomscalepageview = io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_pagelayoutview.
  ENDIF.

  IF io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_sheetlayoutview NE 0.
    IF io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_sheetlayoutview GT 400.
      io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_sheetlayoutview = 400.
    ELSEIF io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_sheetlayoutview LT 10.
      io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_sheetlayoutview = 10.
    ENDIF.
    l_worksheet-zoomscalesheetview = io_worksheet-&gt;zif_excel_sheet_properties~zoomscale_sheetlayoutview.
  ENDIF.

  l_worksheet-workbookviewid = lc_zero.

  IF io_worksheet-&gt;show_gridlines = abap_true.
    l_worksheet-showgridlines = lc_one.
  ELSE.
    l_worksheet-showgridlines = lc_zero.
  ENDIF.

  IF io_worksheet-&gt;show_rowcolheaders = abap_true.
    l_worksheet-showrowcolheaders = lc_one.
  ELSE.
    l_worksheet-showrowcolheaders = lc_zero.
  ENDIF.

*
* Freeze
*
  io_worksheet-&gt;get_freeze_cell(
    IMPORTING ep_row    = lv_freeze_cell_row
              ep_column = lv_freeze_cell_column ).

  IF lv_freeze_cell_row IS NOT INITIAL AND lv_freeze_cell_column IS NOT INITIAL.
    IF lv_freeze_cell_row &gt; 1.
      l_worksheet-ysplit = lv_freeze_cell_row - 1.
    ENDIF.

    IF lv_freeze_cell_column &gt; 1.
      lv_value = lv_freeze_cell_column - 1.
      l_worksheet-xsplit = lv_freeze_cell_row - 1.
    ENDIF.

    lv_freeze_cell_column_alpha = zcl_excel_common=&gt;convert_column2alpha( ip_column = lv_freeze_cell_column ).
    lv_value = zcl_excel_common=&gt;number_to_excel_string( ip_value = lv_freeze_cell_row  ).
    CONCATENATE lv_freeze_cell_column_alpha lv_value INTO lv_value.
    l_worksheet-topleftcell = lv_value.

    l_worksheet-activepane = &apos;bottomRight&apos;.
    l_worksheet-state      = &apos;frozen&apos;.
  ENDIF.

  l_worksheet-activecell = io_worksheet-&gt;get_active_cell( ).

*
* Row and column info
*
  column_dimensions[] = io_worksheet-&gt;get_column_dimensions( ).
  row_dimensions[]    = io_worksheet-&gt;get_row_dimensions( ).

  IF NOT column_dimensions IS INITIAL.
    io_worksheet-&gt;calculate_column_widths( ).
    column_dimensions[] = io_worksheet-&gt;get_column_dimensions( ).
  ENDIF.

  default_row_dimension = io_worksheet-&gt;get_default_row_dimension( ).
  IF default_row_dimension IS BOUND.
    IF default_row_dimension-&gt;get_row_height( ) &gt;= 0.
      l_worksheet-customheight = lc_true.
      lv_value = default_row_dimension-&gt;get_row_height( ).
    ELSE.
      lv_value = &apos;12.75&apos;.
    ENDIF.
  ELSE.
    lv_value = &apos;12.75&apos;.
  ENDIF.
  CONDENSE lv_value.
  l_worksheet-defaultrowheight = lv_value.

  default_col_dimension = io_worksheet-&gt;get_default_column_dimension( ).
  IF default_col_dimension IS BOUND.
    IF default_col_dimension-&gt;get_width( ) &gt;= 0.
      l_worksheet-defaultcolwidth = default_col_dimension-&gt;get_width( ).
    ENDIF.
  ENDIF.

  LOOP AT row_dimensions ASSIGNING &lt;row_dimension&gt;.
    IF &lt;row_dimension&gt;-row_dimension-&gt;get_outline_level( ) &gt; outline_level_row.
      l_worksheet-outlinelevelrow = &lt;row_dimension&gt;-row_dimension-&gt;get_outline_level( ).
    ENDIF.
  ENDLOOP.

  LOOP AT column_dimensions ASSIGNING &lt;column_dimension&gt;.
    IF &lt;column_dimension&gt;-column_dimension-&gt;get_outline_level( ) &gt; outline_level_col.
      l_worksheet-outlinelevelcol = &lt;column_dimension&gt;-column_dimension-&gt;get_outline_level( ).
    ENDIF.
  ENDLOOP.

*
* Set column information (width, style, ...)
*
  LOOP AT column_dimensions ASSIGNING &lt;column_dimension&gt;.
    APPEND INITIAL LINE TO l_worksheet-cols ASSIGNING &lt;col&gt;.
    &lt;col&gt;-min = &lt;col&gt;-max = &lt;column_dimension&gt;-column_dimension-&gt;get_column_index( ).
    &lt;col&gt;-width = &lt;column_dimension&gt;-column_dimension-&gt;get_width( ).
    IF &lt;col&gt;-width &lt; 0.
      &lt;col&gt;-width = lc_default_col_width.
    ENDIF.
    IF &lt;column_dimension&gt;-column_dimension-&gt;get_visible( ) = abap_false.
      &lt;col&gt;-hidden = lc_true.
    ENDIF.
    IF &lt;column_dimension&gt;-column_dimension-&gt;get_auto_size( ) = abap_true.
      &lt;col&gt;-bestfit = lc_true.
    ENDIF.
    IF default_col_dimension IS BOUND.
      IF &lt;column_dimension&gt;-column_dimension-&gt;get_width( )
         &lt;&gt; default_col_dimension-&gt;get_width( ).
        &lt;col&gt;-customwidth = lc_true.
      ENDIF.
    ELSE.
      &lt;col&gt;-customwidth = lc_true.
    ENDIF.
    IF &lt;column_dimension&gt;-column_dimension-&gt;get_collapsed( ) = abap_true.
      &lt;col&gt;-collapsed = lc_true.
    ENDIF.
    &lt;col&gt;-outlinelevel = &lt;column_dimension&gt;-column_dimension-&gt;get_outline_level( ).
    lv_style_guid = &lt;column_dimension&gt;-column_dimension-&gt;get_column_style_guid( ).
    &lt;col&gt;-style = me-&gt;excel-&gt;get_style_index_in_styles( lv_style_guid ) - 1.
  ENDLOOP.

*
* Missing columns
*
* First collect columns that were already handled before.
* The rest has to be inserted now.
*
  LOOP AT column_dimensions ASSIGNING &lt;column_dimension&gt;.
    lv_column = zcl_excel_common=&gt;convert_column2int( &lt;column_dimension&gt;-column ).
    INSERT lv_column INTO TABLE lts_sorted_columns.
  ENDLOOP.

*
* Now find all columns that were missing so far
*
  missing_column-first_column = 1.

  LOOP AT lts_sorted_columns INTO lv_column.
    IF lv_column &gt; missing_column-first_column.
      missing_column-last_column = lv_column - 1.
      APPEND missing_column TO t_missing_columns.
    ENDIF.
    missing_column-first_column = lv_column + 1.
  ENDLOOP.

  missing_column-last_column = zcl_excel_common=&gt;c_excel_sheet_max_col.
  APPEND missing_column TO t_missing_columns.

*
* Now apply stylesetting and other defaults
*
  LOOP AT t_missing_columns INTO missing_column.
    APPEND INITIAL LINE TO l_worksheet-cols ASSIGNING &lt;col&gt;.
    &lt;col&gt;-min = missing_column-first_column.
    &lt;col&gt;-max = missing_column-last_column.
    IF default_col_dimension IS BOUND AND default_col_dimension-&gt;get_width( ) &gt;= 0.
      &lt;col&gt;-width = default_col_dimension-&gt;get_width( ).
    ELSE.
      &lt;col&gt;-width = lc_default_col_width.
    ENDIF.
    lv_style_guid = io_worksheet-&gt;zif_excel_sheet_properties~get_style( ).
    &lt;col&gt;-style = me-&gt;excel-&gt;get_style_index_in_styles( lv_style_guid ) - 1.
  ENDLOOP.

*
* Build table to hold all table-areas attached to this sheet
*
  lo_iterator = io_worksheet-&gt;get_tables_iterator( ).
  WHILE lo_iterator-&gt;if_object_collection_iterator~has_next( ) EQ abap_true.
    lo_table ?= lo_iterator-&gt;if_object_collection_iterator~get_next( ).
    ls_table_area-left   = zcl_excel_common=&gt;convert_column2int( lo_table-&gt;settings-top_left_column ).
    ls_table_area-right  = lo_table-&gt;get_right_column_integer( ).
    ls_table_area-top    = lo_table-&gt;settings-top_left_row.
    ls_table_area-bottom = lo_table-&gt;get_bottom_row_integer( ).
    INSERT ls_table_area INTO TABLE lt_table_areas.
  ENDWHILE.

*
* Build sheet data node
*
* Spans is constant amongst all rows
*
  col_count = io_worksheet-&gt;get_highest_column( ).
  lv_spans = col_count.
  CONCATENATE &apos;1:&apos; lv_spans INTO lv_spans.
  CONDENSE lv_spans.

  LOOP AT io_worksheet-&gt;sheet_content ASSIGNING &lt;sheet_content&gt;.

    IF ls_last_row-cell_row NE &lt;sheet_content&gt;-cell_row.
*
*     Fill row information.
*     Cell data is filled in by callback GET_CELLS called from transformation
*
      lv_index = sy-tabix.
      APPEND INITIAL LINE TO l_worksheet-rows ASSIGNING &lt;row&gt;.
      &lt;row&gt;-row   = &lt;sheet_content&gt;-cell_row.
      &lt;row&gt;-index = lv_index.
      &lt;row&gt;-spans = lv_spans.

*
*     Row dimension attributes
*
      row_dimension = io_worksheet-&gt;get_row_dimension( &lt;sheet_content&gt;-cell_row ).
      IF row_dimension-&gt;get_visible( ) = abap_false.
        &lt;row&gt;-hidden = lc_true.
      ENDIF.

      IF row_dimension-&gt;get_row_height( ) &gt;= 0.
        &lt;row&gt;-customheight = lc_one.
        &lt;row&gt;-height       = row_dimension-&gt;get_row_height( ).
      ENDIF.

*
*     Collapsed
*
      IF row_dimension-&gt;get_collapsed( ) = abap_true.
        &lt;row&gt;-collapsed = lc_true.
      ENDIF.

*
*     Outline level
*
      &lt;row&gt;-outlinelevel = row_dimension-&gt;get_outline_level( ).

*
*     Style
*
      &lt;row&gt;-style = row_dimension-&gt;get_xf_index( ).
      IF &lt;row&gt;-style &lt;&gt; 0.
        &lt;row&gt;-customformat = lc_one.
      ENDIF.
    ENDIF.

    ls_last_row = &lt;sheet_content&gt;.
  ENDLOOP.

*
* Merged cells
*
  lt_range_merge = io_worksheet-&gt;get_merge( ).
  IF lt_range_merge IS NOT INITIAL.
    l_worksheet-mergecells_count = lines( lt_range_merge ).

    LOOP AT lt_range_merge ASSIGNING &lt;range_merge&gt;.
      APPEND INITIAL LINE TO l_worksheet-mergecells ASSIGNING &lt;mergecell&gt;.
      &lt;mergecell&gt;-ref = &lt;range_merge&gt;.
      io_worksheet-&gt;delete_merge( ).
    ENDLOOP.
  ENDIF.

*
* Hyperlinks
*
  l_worksheet-hyperlinks_count = io_worksheet-&gt;get_hyperlinks_size( ).
  IF l_worksheet-hyperlinks_count &gt; 0.
    lo_iterator = io_worksheet-&gt;get_hyperlinks_iterator( ).
    WHILE lo_iterator-&gt;if_object_collection_iterator~has_next( ) EQ abap_true.
      lo_link ?= lo_iterator-&gt;if_object_collection_iterator~get_next( ).

      APPEND INITIAL LINE TO l_worksheet-hyperlinks ASSIGNING &lt;hyperlink&gt;.
      &lt;hyperlink&gt;-ref = lo_link-&gt;get_ref( ).
      IF lo_link-&gt;is_internal( ) = abap_true.
        &lt;hyperlink&gt;-location = lo_link-&gt;get_url( ).
      ELSE.
        ADD 1 TO lv_relation_id.
        lv_value = lv_relation_id.
        CONDENSE lv_value.
        CONCATENATE &apos;rId&apos; lv_value INTO lv_value.
        &lt;hyperlink&gt;-r_id = lv_value.
      ENDIF.
    ENDWHILE.
  ENDIF.

*
* Print options
*
  IF io_worksheet-&gt;print_gridlines = abap_true.
    l_worksheet-printoptions-gridlines = lc_true.
  ENDIF.

  IF io_worksheet-&gt;sheet_setup-&gt;horizontal_centered = abap_true.
    l_worksheet-printoptions-horizontalcentered = lc_true.
  ENDIF.

  IF io_worksheet-&gt;sheet_setup-&gt;vertical_centered = abap_true.
    l_worksheet-printoptions-verticalcentered = lc_true.
  ENDIF.

*
* Page margins
*
  l_worksheet-pagemargins-left   = io_worksheet-&gt;sheet_setup-&gt;margin_left.
  l_worksheet-pagemargins-right  = io_worksheet-&gt;sheet_setup-&gt;margin_right.
  l_worksheet-pagemargins-top    = io_worksheet-&gt;sheet_setup-&gt;margin_top.
  l_worksheet-pagemargins-bottom = io_worksheet-&gt;sheet_setup-&gt;margin_bottom.
  l_worksheet-pagemargins-header = io_worksheet-&gt;sheet_setup-&gt;margin_header.
  l_worksheet-pagemargins-footer = io_worksheet-&gt;sheet_setup-&gt;margin_footer.

*
* Page setup
*
  l_worksheet-pagesetup-cellcomments       = io_worksheet-&gt;sheet_setup-&gt;cell_comments.
  l_worksheet-pagesetup-copies             = io_worksheet-&gt;sheet_setup-&gt;copies.
  l_worksheet-pagesetup-firstpagenumber    = io_worksheet-&gt;sheet_setup-&gt;first_page_number.
  l_worksheet-pagesetup-fittopage          = io_worksheet-&gt;sheet_setup-&gt;fit_to_page.
  l_worksheet-pagesetup-fittoheight        = io_worksheet-&gt;sheet_setup-&gt;fit_to_height.
  l_worksheet-pagesetup-fittowidth         = io_worksheet-&gt;sheet_setup-&gt;fit_to_width.
  l_worksheet-pagesetup-horizontaldpi      = io_worksheet-&gt;sheet_setup-&gt;horizontal_dpi.
  l_worksheet-pagesetup-orientation        = io_worksheet-&gt;sheet_setup-&gt;orientation.
  l_worksheet-pagesetup-pageorder          = io_worksheet-&gt;sheet_setup-&gt;page_order.
  l_worksheet-pagesetup-paperheight        = io_worksheet-&gt;sheet_setup-&gt;paper_height.
  l_worksheet-pagesetup-papersize          = io_worksheet-&gt;sheet_setup-&gt;paper_size.
  l_worksheet-pagesetup-paperwidth         = io_worksheet-&gt;sheet_setup-&gt;paper_width.
  l_worksheet-pagesetup-scale              = io_worksheet-&gt;sheet_setup-&gt;scale.
  l_worksheet-pagesetup-usefirstpagenumber = io_worksheet-&gt;sheet_setup-&gt;use_first_page_num.
  l_worksheet-pagesetup-verticaldpi        = io_worksheet-&gt;sheet_setup-&gt;vertical_dpi.

  IF io_worksheet-&gt;sheet_setup-&gt;black_and_white IS NOT INITIAL.
    l_worksheet-pagesetup-blackandwhite = lc_one.
  ENDIF.

  IF io_worksheet-&gt;sheet_setup-&gt;draft IS NOT INITIAL.
    l_worksheet-pagesetup-draft = lc_one.
  ENDIF.

  IF io_worksheet-&gt;sheet_setup-&gt;errors IS NOT INITIAL.
    l_worksheet-pagesetup-errors = io_worksheet-&gt;sheet_setup-&gt;errors.
  ENDIF.

  IF io_worksheet-&gt;sheet_setup-&gt;use_printer_defaults IS NOT INITIAL.
    l_worksheet-pagesetup-useprinterdefaults = lc_one.
  ENDIF.

*
* Header and footer
*
  IF io_worksheet-&gt;sheet_setup-&gt;diff_oddeven_headerfooter = abap_true.
    l_worksheet-headerfooter-differentoddeven = lc_one.
  ENDIF.

  io_worksheet-&gt;sheet_setup-&gt;get_header_footer_string(
    IMPORTING
      ep_odd_header  = l_worksheet-headerfooter-oddheader
      ep_odd_footer  = l_worksheet-headerfooter-oddfooter
      ep_even_header = l_worksheet-headerfooter-evenheader
      ep_even_footer = l_worksheet-headerfooter-evenfooter ).

*
* Drawings
*
  lo_drawings = io_worksheet-&gt;get_drawings( ).
  IF lo_drawings-&gt;is_empty( ) = abap_false.
    ADD 1 TO lv_relation_id.
    lv_value = lv_relation_id.
    CONDENSE lv_value.
    CONCATENATE &apos;rId&apos; lv_value INTO l_worksheet-drawings.
  ENDIF.

*
* Tables
*
  l_worksheet-tables_count = io_worksheet-&gt;get_tables_size( ).
  IF l_worksheet-tables_count &gt; 0.
    lo_iterator = io_worksheet-&gt;get_tables_iterator( ).
    WHILE lo_iterator-&gt;if_object_collection_iterator~has_next( ) EQ abap_true.
      lo_table ?= lo_iterator-&gt;if_object_collection_iterator~get_next( ).
      APPEND INITIAL LINE TO l_worksheet-tables ASSIGNING &lt;table&gt;.
      ADD 1 TO lv_relation_id.
      lv_value = lv_relation_id.
      CONDENSE lv_value.
      CONCATENATE &apos;rId&apos; lv_value INTO &lt;table&gt;-r_id.
    ENDWHILE.
  ENDIF.

**********************************************************************
* STEP 2: Create XML

  me-&gt;worksheet = io_worksheet. &quot;Neccessary for callback GET_CELL

  CALL TRANSFORMATION zexcel_tr_sheet
    SOURCE worksheet = l_worksheet
           cells     = me-&gt;cells
           writer    = me
    OPTIONS xml_header = &apos;full&apos;
    RESULT XML ep_content.

ENDMETHOD.                    &quot;CREATE_XL_SHEET</source>
 </method>
 <method CLSNAME="ZCL_EXCEL_WRITER_HUGE_FILE" CMPNAME="GET_CELLS" VERSION="1" LANGU="D" DESCRIPT="Read the cell data for a single row, called from transform." EXPOSURE="2" STATE="1" EDITORDER="1 " DISPID="0 " MTDTYPE="0" MTDDECLTYP="0" BCMTDCAT="00" BCMTDSYN="0">
  <parameter CLSNAME="ZCL_EXCEL_WRITER_HUGE_FILE" CMPNAME="GET_CELLS" SCONAME="I_ROW" VERSION="1" LANGU="D" DESCRIPT="Current row" CMPTYPE="1" MTDTYPE="0" EDITORDER="1 " DISPID="0 " PARDECLTYP="0" PARPASSTYP="1" TYPTYPE="1" TYPE="I"/>
  <parameter CLSNAME="ZCL_EXCEL_WRITER_HUGE_FILE" CMPNAME="GET_CELLS" SCONAME="I_INDEX" VERSION="1" LANGU="D" DESCRIPT="index into sheet content" CMPTYPE="1" MTDTYPE="0" EDITORDER="2 " DISPID="0 " PARDECLTYP="0" PARPASSTYP="1" TYPTYPE="1" TYPE="I"/>
  <source>METHOD get_cells.
*
* Callback method from transformation ZEXCEL_TR_SHEET
*
* The method fills the data cells for each row.
* This saves memory if there are many rows.
*
  DATA:
    lv_cell_style TYPE zexcel_cell_style.

  FIELD-SYMBOLS:
    &lt;cell&gt;        TYPE ty_cell,
    &lt;content&gt;     TYPE zexcel_s_cell_data,
    &lt;style&gt;       TYPE zexcel_s_styles_mapping.

  CLEAR cells.

  LOOP AT worksheet-&gt;sheet_content FROM i_index ASSIGNING &lt;content&gt;.
    IF &lt;content&gt;-cell_row &lt;&gt; i_row.
*
*     End of row
*
      EXIT.
    ENDIF.

*
*   Determine style index
*
    IF lv_cell_style &lt;&gt; &lt;content&gt;-cell_style.
      lv_cell_style = &lt;content&gt;-cell_style.
      UNASSIGN &lt;style&gt;.
      IF lv_cell_style IS NOT INITIAL.
        READ TABLE styles_mapping ASSIGNING &lt;style&gt; WITH KEY guid = lv_cell_style.
      ENDIF.
    ENDIF.
*
*   Add a new cell
*
    APPEND INITIAL LINE TO cells ASSIGNING &lt;cell&gt;.
    &lt;cell&gt;-name    = &lt;content&gt;-cell_coords.
    &lt;cell&gt;-formula = &lt;content&gt;-cell_formula.
    &lt;cell&gt;-type    = &lt;content&gt;-data_type.
    IF &lt;cell&gt;-type = &apos;s&apos;.
      &lt;cell&gt;-value = me-&gt;get_shared_string_index( &lt;content&gt;-cell_value ).
    ELSE.
      &lt;cell&gt;-value = &lt;content&gt;-cell_value.
    ENDIF.
    IF &lt;style&gt; IS ASSIGNED.
      &lt;cell&gt;-style = &lt;style&gt;-style.
    ELSE.
      &lt;cell&gt;-style = -1.
    ENDIF.

  ENDLOOP.

ENDMETHOD.</source>
 </method>
</CLAS>
